load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")
load("@rules_python//python:defs.bzl", "py_library")

filegroup(name = "all", srcs = glob(["**"], exclude = ["bitmaps_png/sources/**"]))

cmake(
    name = "kicad_build",
    build_args = ["-j2"],
    cache_entries = {
        "CMAKE_BUILD_TYPE": "Release",
        "KICAD_SPICE": "OFF",
        "KICAD_USE_OCE": "OFF",
        "KICAD_SCRIPTING_PYTHON3": "ON",
        "KICAD_SCRIPTING_WXPYTHON": "OFF",
    },
    lib_source = ":all",
    out_shared_libs = ["libkicad_3dsg.so.2.0.0"],
)

filegroup(
    name = "gen_dir",
    srcs = [":kicad_build"],
    output_group = "gen_dir",
)

genrule(
    name = "pcbnew_py",
    srcs = [":gen_dir"],
    outs = ["pcbnew.py"],
    cmd = "cp \"$(location :gen_dir)/lib/python3/dist-packages/pcbnew.py\" \"$@\"",
)

genrule(
    name = "pcbnew_so",
    srcs = [":gen_dir"],
    outs = ["_pcbnew.so"],
    cmd = "cp \"$(location :gen_dir)/lib/python3/dist-packages/_pcbnew.so\" \"$@\"",
)

genrule(
    name = "libkicad_3dsg_so",
    srcs = [":gen_dir"],
    outs = ["libkicad_3dsg.so.2.0.0"],
    cmd = "cp \"$(location :gen_dir)/lib/libkicad_3dsg.so.2.0.0\" \"$@\"",
)

py_library(
    name = "pcbnew",
    srcs = [":pcbnew_py"],
    data = [":pcbnew_so", ":libkicad_3dsg_so"],
    visibility = ["//visibility:public"],
)
